project.description = 'This package contains functionalities to update application asset files.'
evaluationDependsOn(':sdk')

apply plugin: "com.android.library"
apply plugin: 'jacoco'

jacoco {
    toolVersion '0.7.4.201502262128'
}

android {
    compileSdkVersion 23
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
        explainIssues true
        textReport true
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }

        test {
            java.srcDirs += "$projectDir/src/sharedTest/java"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/sharedTest/java"
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify'
            }
            returnDefaultValues = true
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
    }

}

gradle.projectsEvaluated {
    android.buildTypes.each {
        // to prevent incorrect long value restoration from strings.xml we need to wrap it with double quotes
        // https://github.com/Microsoft/cordova-plugin-code-push/issues/264
        it.resValue 'string', "ASSETS_APK_BUILD_TIME", String.format("\"%d\"", System.currentTimeMillis())
    }
}

dependencies {
    api project(':sdk:appcenter')
    compileOnly "com.android.support:support-annotations:+"
    compile 'com.nimbusds:nimbus-jose-jwt:5.3'
    compile "com.google.code.gson:gson:2.8.2"
}
